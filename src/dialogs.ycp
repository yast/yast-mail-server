/**
 * File:	include/mail-server/dialogs.ycp
 * Package:	Configuration of mail-server
 * Summary:	Dialogs definitions
 * Authors:	Peter Varkoly <varkoly@suse.de>
 *
 * $Id$
 */

{

textdomain "MailServer";

import "Label";
import "Wizard";
import "YaPI::MailServer";

include "mail-server/helps.ycp";

/**
 * AuthorizingDialog
 * The adminstrator user will be authorized
 * @return dialog result
 */
define any AuthorizingDialog () {
    string caption = _("Mail Server Administrator Authorization.");
    term contents = `HBox(
			   `VBox(
                              `Password(`id(`password),  _("Enter Administrator Password:"), "")
			        )
                         );

    Wizard::SetContentsButtons(caption, contents, HELPS["c1"]:"",
	    Label::BackButton(), Label::NextButton());
    UI::WizardCommand(`SetBackButtonLabel( "" ) );
    UI::SetFocus (`id (`password));

    any ret = nil;
    while(true) {

        ret = UI::UserInput();

        MailServer::AdminPassword  = (string) UI::QueryWidget(`id(`password), `Value);

        /* abort? */
        if(ret == `abort || ret == `cancel) {
            if(ReallyAbort()) break;
            else continue;
        } else if(ret == `next) {
                MailServer::LDAPDefaults = (map<string,any>) YaPI::MailServer::ReadLDAPDefaults(
                                                                MailServer::AdminPassword);
                if (MailServer::LDAPDefaults == nil) {
			Popup::Error(_("Unable to authorize the Administrator User"));
			continue;
		}
                return ret;
	} else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;

}

list<map> GenerateTree(list<map> Tree, string parent, list<list> input) {

    foreach(list i, input, ``{
            string title = i[0]:"";
            list<list> children = i[1]:[];
            Tree = Wizard::AddTreeItem(Tree , parent,  title, title );
            if (size(children) > 0 ) {
                Tree = GenerateTree(Tree, title, children);
            }
    });
    return Tree;
}

term GlobalSettingsDialog() {
  boolean limit     = ((string)MailServer::GlobalSettings["MaximumMailSize"]:"0" != "0");
  boolean is_DNS    = ((string)MailServer::GlobalSettings["SendingMail","Type"]:"" == "DNS");
  boolean is_NONE   = ((string)MailServer::GlobalSettings["SendingMail","Type"]:"" == "NONE");
  boolean is_RELAY  = ((string)MailServer::GlobalSettings["SendingMail","Type"]:"" == "relayhost");
  boolean TLS_NONE  = ((string)MailServer::GlobalSettings["SendingMail","TLS"]:""  == "NONE");
  boolean TLS_MAY   = ((string)MailServer::GlobalSettings["SendingMail","TLS"]:""  == "MAY");
  boolean TLS_MUST  = ((string)MailServer::GlobalSettings["SendingMail","TLS"]:""  == "MUST");
  boolean TLS_MUSTN = ((string)MailServer::GlobalSettings["SendingMail","TLS"]:""  == "MUST_NOPEERMATCH");
  boolean auth      = ((string)MailServer::GlobalSettings["SendingMail","RelayHost","Auth"]:"0" == "1");
  string  hostname  = (string)MailServer::GlobalSettings["SendingMail","RelayHost","Name"]:"";
  string  account   = (string)MailServer::GlobalSettings["SendingMail","RelayHost","Account"]:"";
  string  password  = (string)MailServer::GlobalSettings["SendingMail","RelayHost","Password"]:"";

  // I don't know if it hase a sence to use MUST_NOPEERMATCH.
  if(TLS_MUSTN){ TLS_MUST = TLS_MUSTN; }


  term  relayhost =  `VBox(
                    `TextEntry(`id(`RelayHostName), _("Hostname / IP"), hostname),
                    `CheckBox(`id(`RelayHostAuth), `opt (`notify),  _("Relay host need Authentication"), auth),
                    `Frame("",
                      `VBox(
                        `TextEntry(`id(`RelayHostAccount),  _("Account"), account),
                        `Password(`id(`Password1),  _("Password"), password),
                        `Password(`id(`Password2),  _("Confirm Passowrd"), password)
                       )
                     )
                   );
  if( is_RELAY && ! auth) {
       relayhost =  `VBox(
                    `TextEntry(`id(`RelayHostName), _("Hostname / IP"), hostname),
                    `CheckBox(`id(`RelayHostAuth),  `opt (`notify), _("Relay host need Authentication"), auth),
                    `Frame("",
                      `VBox(
                        `TextEntry(`id(`RelayHostAccount),  `opt(`disabled), _("Account"), account),
                        `Password(`id(`Password1), `opt(`disabled), _("Password"), password),
                        `Password(`id(`Password2), `opt(`disabled), _("Confirm Passowrd"), password)
                       )
                     )
                   );
  } else if( ! is_RELAY ) {
       relayhost =  `VBox(
                    `TextEntry(`id(`RelayHostName), `opt(`disabled), _("Hostname / IP"), hostname),
                    `CheckBox(`id(`RelayHostAuth),      `opt(`disabled), _("Relay host need Authentication"), auth),
                    `Frame("",
                      `VBox(
                        `TextEntry(`id(`RelayHostAccount),  `opt(`disabled), _("Account"), account),
                        `Password(`id(`Password1), `opt(`disabled), _("Password"), password),
                        `Password(`id(`Password2), `opt(`disabled), _("Confirm Passowrd"), password)
                       )
                     )
                   );
  }

  term MaximumMailSize = `TextEntry(`id(`MaximumMailSize), "",(string)MailServer::GlobalSettings["MaximumMailSize"]:"");
  if(!limit) {
    MaximumMailSize = `TextEntry(`id(`MaximumMailSize), `opt(`disabled),"",(string)MailServer::GlobalSettings["MaximumMailSize"]:"");
  }

  term content = `VBox (
       `Frame(_("Message Size"),
         `RadioButtonGroup(`id(`MailSize),
           `HBox(
             `RadioButton(`id("MailSizeNoLimit"), `opt (`notify), _("No Limit"), !limit),
             `RadioButton(`id("MailSizeLimit"), `opt (`notify), _("Maximum Mail Size"), limit),
             MaximumMailSize,
             `Label("KByte")
            )
          )
        ),
       `VStretch(),
       `Frame(_("Server Identification"),
            `TextEntry(`id(`Banner), "" ,(string)MailServer::GlobalSettings["Banner"]:"SuSE Linux Mail Server")
        ),
       `VStretch(),
       `Frame(_("Outgoing Mails"),
         `HBox(
           `HWeight(1,
              `VBox(
                `VStretch(),
                `RadioButtonGroup(`id(`SendingMailType),
                  `VBox(
                    `Left(`RadioButton(`id("relayhost"), `opt (`notify), _("Use Relay Host"), is_RELAY)),
                    `Left(`RadioButton(`id("DNS"),       `opt (`notify), _("Direct Delivery (DNS)"), is_DNS)),
                    `Left(`RadioButton(`id("NONE"),      `opt (`notify), _("No Outgoing Mail"), is_NONE))
                   )
                 ),
                `VStretch(),
                `RadioButtonGroup(`id(`SendingMailTLS),
                   `HBox(
                      `Frame(_("Security"),
                        `VBox(
                           `Left(`RadioButton(`id("NONE"),             _("Do not use TLS"), TLS_NONE)),
                           `Left(`RadioButton(`id("MAY"),              _("Use TLS if Possible"), TLS_MAY)),
                           `Left(`RadioButton(`id("MUST"),             _("Enforce TLS"), TLS_MUST))
      //                  ,`Left(`RadioButton(`id("MUST_NOPEERMATCH"), _("Use TLS but do not check CA"), TLS_MUSTN))
                         )
                      ),
                      `HStretch()
                   )
                 ),
                `VStretch()
               )
            ),
           `HWeight(1,
              `VBox(
                `Frame(_("Relay Host Settings"),relayhost
                 )
               )
            )
          )
        )
   );

  return content;
}

term MailTransportsDialog(string CID) {
  list<map<string,string> > Transports      = (list<map<string,string> >) MailServer::MailTransports["Transports"]:[];
  list      Transport_items = [];
  string    TransportID     = "";
  string    Destination     = "";
  boolean   Subdomains      = true;
  string    Protocol        = "smtp";
  map<string,list<term> > Protocols = $[ "smtp":[ `item(`id("smtp"),  "smtp", true ),
                                          `item(`id("uucp"),  "uucp" ),
                                          `item(`id("error"), "error" ) ],
				 "uucp":[ `item(`id("smtp"),  "smtp" ),
                                          `item(`id("uucp"),  "uucp", true ),
                                          `item(`id("error"), "error" ) ],
				 "error":[`item(`id("smtp"),  "smtp" ),
                                          `item(`id("uucp"),  "uucp" ),
                                          `item(`id("error"), "error", true ) ]
			      ];
  string    Server          = "";
  boolean   NOMX            = true;
  string    TLS             = "";
  boolean   Auth            = false;
  string    Account         = "";
  string    Password        = "";
  boolean   TransportTLSnone    = false;
  boolean   TransportTLSuse     = true;
  boolean   TransportTLSenforce = false;


  foreach(map Transport,Transports, ``{
    TransportID = Transport["Destination"]:"";
//    term my_item = `item( `id(TransportID), Transport["Destination"]:""+" | "+
//                                            Transport["Nexthop"]:""+" | "+
//                                            Transport["Transport"]:""+" | "+
//					    Transport["TLS"]:"");
    term my_item = `item( `id(TransportID), Transport["Destination"]:"",
                                            Transport["Nexthop"]:"",
                                            Transport["Transport"]:"",
                                            Transport["TLS"]:"");

    if(CID == TransportID) {
      Destination = Transport["Destination"]:"";
      TLS         = Transport["TLS"]:"";
      Auth        = ((string)Transport["Auth"]:"0" == "1");
      Account     = Transport["Account"]:"";
      Password    = Transport["Password"]:"";
      if( find(Transport["Nexthop"]:"", "\[") > -1) {
        Server = deletechars(Transport["Nexthop"]:"","[");
        Server = deletechars(Server,"]");
      } else {
        Server = Transport["Nexthop"]:"";
	NOMX   = false;
      }
      if(TLS == "NONE") {
        TransportTLSnone    = true;
	TransportTLSuse     = false;
	TransportTLSenforce = false;
      }else if(TLS == "MUST"){
        TransportTLSnone    = false;
	TransportTLSuse     = false;
	TransportTLSenforce = true;
      }
//      my_item = `item( `id(TransportID), Transport["Destination"]:""+" | "+
//                                         Transport["Nexthop"]:""+" | "+
//                                         Transport["Transport"]:""+" | "+
//                                         Transport["TLS"]:"",true);
    }
    Transport_items = add(Transport_items,my_item);
  });

  term TransportAuth  = `VBox(
    `Left(`CheckBox (`id(`Auth),     `opt (`notify), _("Server need Authentication"), true)),
    `Left(`TextEntry(`id(`Account),   _("Account"),          Account)),
    `Left(`Password (`id(`Password1), _("Password"),         Password)),
    `Left(`Password (`id(`Password2), _("Confirm Passowrd"), Password))
  );
  if(!Auth) {
    TransportAuth  = `VBox(
      `Left(`CheckBox (`id(`Auth),      `opt (`notify),  _("Server need Authentication"),       false)),
      `Left(`TextEntry(`id(`Account),   `opt(`disabled), _("Account"),          Account)),
      `Left(`Password (`id(`Password1), `opt(`disabled), _("Password"),         Password)),
      `Left(`Password (`id(`Password2), `opt(`disabled), _("Confirm Passowrd"), Password))
    );
  }


  term content = `VBox (
     `Frame(_("Manage Mail Routing"),
       `VBox(
         `HBox(
           `VBox(
             `Left(`TextEntry(`id(`Destination), _("Destination"),     Destination)),
             `Left(`CheckBox (`id(`Subdomains),  _("With Subdomains"), Subdomains)),
             `Left(`HBox(`Label(_("Transport")),`ComboBox (`id(`Protocol), "", Protocols[Protocol]:nil))),
             `Left(`TextEntry(`id(`Server),      _("Server"),     Server)),
             `Left(`CheckBox (`id(`NOMX),        _("Suppress MX Lookups"), NOMX))
            ),
           `Frame(_("Security"),
             `HBox(
               `RadioButtonGroup(`id(`TLS),
                 `VBox(
                   `Left(`RadioButton(`id("NONE"),  _("No TLS"),     TransportTLSnone)),
                   `Left(`RadioButton(`id("MAY"),   _("Use TLS"),    TransportTLSuse)),
                   `Left(`RadioButton(`id("MUST"),  _("Enforce TLS"),TransportTLSenforce))
                  )
                ),
                TransportAuth
              )
            )
          ),
         `HBox(
           `PushButton(`id(`Add),   _("Add")),
           `PushButton(`id(`Edit),  _("Edit")),
           `PushButton(`id(`Delete),_("Delete")),
           `PushButton(`id(`Clean), _("Clean Fields"))
          )
        )
      ),
     `Frame(_("Defined Mail Transport Routen"),
       `HBox(
         //`SelectionBox(`id(`Table), `opt (`notify),_("Destination | Server | Transport | Security"),Transport_items)
         `Table(`id(`table), `opt(`immediate,`keepSorting,`notify),`header(_("Destination"),_("Server"),_("Transport"),_("Security")),Transport_items)
        )
      )
   );
  return content;
}

term MailPreventionDialog(string CID, string CIDRBL) {
  list<term>     AccessItems= [];
  list<term>     RBLItems   = [];
  string  BasicProtection   =  (string)MailServer::MailPrevention["BasicProtection"]:"";
  list<string> RBLList      = (list<string>) MailServer::MailPrevention["RBLList"]:[];
  list<map<string,string> > AccessList = (list<map<string,string> >) MailServer::MailPrevention["AccessList"]:[];
  boolean VirusScanning     = ((string)MailServer::MailPrevention["VirusScanning"]:"0" == "1");
  boolean BasicProtectionOff    = false;
  boolean BasicProtectionMedium = false;
  boolean BasicProtectionHard   = true;
  if(BasicProtection == "off"){
     BasicProtectionOff     = true;
     BasicProtectionHard    = false;
  } else if(BasicProtection == "medium"){
     BasicProtectionMedium  = true;
     BasicProtectionHard    = false;
  }

  foreach(map<string,string> Access, AccessList, ``{
    term my_item = `item( `id(Access["MailClient"]:""), Access["MailClient"]:"", 
                                                        select(splitstring(Access["MailAction"]:""," "),0,""),
							substring(Access["MailAction"]:"",find(Access["MailAction"]:""," "))
                        );
    AccessItems = add(AccessItems,my_item);
  });

  foreach(string RBLServer, RBLList, ``{
    term my_item = `item( `id(RBLServer), RBLServer);
    RBLItems     = add(RBLItems,my_item);
  });

  term content = `VBox (
	`Frame(_("SPAM Prevention"),
	  `HBox(
	    `RadioButtonGroup(`id(`BasicProtection),
	      `VBox(
	        `Left(`RadioButton(`id("off"),    `opt(`notify), _("Off"),    BasicProtectionOff)),
	        `Left(`RadioButton(`id("medium"), `opt(`notify), _("Medium"), BasicProtectionMedium)),
	        `Left(`RadioButton(`id("hard"),   `opt(`notify), _("Hard"),   BasicProtectionHard))
	       )
	     ),
	      `VBox(`SelectionBox(`id(`RBLList), "Confiugred RBL Server",RBLItems)
	     ),
	     `VBox(
	        `Left(`PushButton(`id(`RBLAdd),   _("Add"))),
		`Left(`PushButton(`id(`RBLDelete),_("Delete")))
	     )
	   )
         ),
	`Frame(_("Sender Restrictions"),
	  `HBox(
	      `VBox(
	        `Table(`id(`table), `opt(`immediate,`keepSorting,`notify),`header(_("Sender Address"),_("Action"),_("Option")),AccessItems)
	       ),
	      `VBox(
	        `PushButton(`id(`AccessAdd),   _("Add")),
		`PushButton(`id(`AccessDelete),_("Delete"))
	       )
	   )
	 ),
	`Frame(_("Virus Scanning"),
	  `HBox(
               `Left(`CheckBox (`id(`VirusScanning), `opt(`notify), _("Start Virus Scanner AMAVIS"), VirusScanning))
	   )
	 )
   );
   return content;
}

string RBLAdd () {
    UI::OpenDialog(`opt(`decorated ), `Frame(_("Add New RBL Server"),
            `VBox(
              `TextEntry(`id(`server),  "",  ""),
              `HBox(
	         `PushButton(`id(`cancel),   _("Cancel")),
	         `PushButton(`id(`ok),   _("OK"))
               )
	     )
	   )
    );
    any    ret       = nil;
    string server    = "";
    while(ret != `cancel && ret != `ok )
    {
        map event = UI::WaitForEvent();
        ret       = event["ID"]:nil;
	if( ret == `ok) {
	   server = (string)UI::QueryWidget(`id(`server), `Value);
	}
    }
    UI::CloseDialog();
    return server;
}

map AccessAdd () {
   UI::OpenDialog(`opt(`decorated ),  `Frame(_("Add New Sender Restriction"),
            `VBox(
              `TextEntry(`id(`NewSender),  "Sender Address",  ""),
	      `ComboBox (`id(`NewSenderAction), _("Action"), [ "REJECT", "DEFER_IF_REJECT","DEFER_IF_PERMIT",
	                                                       "OK", "DUNNO", "PREPEND", "HOLD", "DISCARD",
							       "FILTER", "REDIRECT", "" ]),
              `TextEntry(`id(`NewSenderOption),  "Option",  ""),
              `HBox(
	         `PushButton(`id(`cancel),   _("Cancel")),
	         `PushButton(`id(`ok),   _("OK"))
               )
	     )
	   )
    );
    any    ret               = nil;
    map    SenderRestriction = $[];
    while(ret != `cancel && ret != `ok )
    {
        map event = UI::WaitForEvent();
        ret       = event["ID"]:nil;
	if( ret == `ok) {
	    string NewSender       = (string)UI::QueryWidget(`id(`NewSender),       `Value);
	    string NewSenderAction = (string)UI::QueryWidget(`id(`NewSenderAction), `Value);
	    string NewSenderOption = (string)UI::QueryWidget(`id(`NewSenderOption), `Value);
	        SenderRestriction  = $[  "MailClient":NewSender,
	                                 "MailAction":NewSenderAction + " " + NewSenderOption
	                              ];
        }
    }
    UI::CloseDialog();
    return SenderRestriction;
}

term MailRelayingDialog() {
  boolean UserRestriction = ((string)MailServer::MailRelaying["UserRestriction"]:"0" == "1");
  boolean RequireSASL     = ((string)MailServer::MailRelaying["RequireSASL"]:"0" == "1");
  boolean SMTPDTLSnone    = ((string)MailServer::MailRelaying["SMTPDTLSMode"]:"none" == "none");
  boolean SMTPDTLSuse     = ((string)MailServer::MailRelaying["SMTPDTLSMode"]:"none" == "use");
  boolean SMTPDTLSenforce = ((string)MailServer::MailRelaying["SMTPDTLSMode"]:"none" == "enforce");
  boolean SMTPDTLSauth_only = ((string)MailServer::MailRelaying["SMTPDTLSMode"]:"none" == "auth_only");
  list<string> Networks     = (list<string>) MailServer::MailRelaying["TrustedNetworks"]:[];
  list    Network_items     = [];
  integer NetworkID         = 1;

  foreach(string Network, Networks, ``{
    term my_item = `item( `id(NetworkID), Network);
    Network_items = add(Network_items,my_item);
    NetworkID = NetworkID+1;
  });

   term security = `HBox(
        `VBox(
          `Left(`CheckBox(`id(`UserRestriction),`opt (`notify),_("Use User/Group Based Restrictions"),UserRestriction)),
          `Left(`CheckBox(`id(`RequireSASL),_("Require SASL Authentication"),RequireSASL))
         ),
        `Frame(_("TLS Mode for the SMTPD Daemon"),
          `RadioButtonGroup(`id(`SMTPDTLSMode),
             `VBox(
                `Left(`RadioButton(`id("none"),     _("No TLS"),SMTPDTLSnone)),
                `Left(`RadioButton(`id("use"),      _("Use TLS"),SMTPDTLSuse)),
                `Left(`RadioButton(`id("enforce"),  _("Enforce TLS"),SMTPDTLSenforce)),
                `Left(`RadioButton(`id("auth_only"),_("Use TLS only for SASL authentication"),SMTPDTLSauth_only))
              )
           )
         )
      );
  if(UserRestriction){
    security = `HBox(
        `VBox(
          `Left(`CheckBox(`id(`UserRestriction),`opt (`notify), _("Use User/Group Based Restrictions"),true)),
          `Left(`CheckBox(`id(`RequireSASL),    `opt(`disabled),_("Require SASL Authentication"),true))
         ),
        `Frame(_("TLS Mode for the SMTPD Daemon"),
          `RadioButtonGroup(`id(`SMTPDTLSMode),
             `VBox(
                `Left(`RadioButton(`id("none"),     `opt(`disabled), _("No TLS"),     false)),
                `Left(`RadioButton(`id("use"),      `opt(`disabled), _("Use TLS"),    false)),
                `Left(`RadioButton(`id("enforce"),  _("Enforce TLS"),SMTPDTLSenforce)),
                `Left(`RadioButton(`id("auth_only"),_("Use TLS only for SASL authentication"),SMTPDTLSauth_only))
              )
           )
         )
      );
  }
  term content = `VBox (
     `Frame(_("Trusted Local Networks"),
       `VBox(
         `Frame(_("New Network"),
           `HBox(
               `TextEntry(`id(`NewNetwork),     "", ""),
               `PushButton(`id(`AddNewNetwork), _("Add"))
            )
          ),
         `Frame(_("Defined Trusted Networks"),
           `HBox(
//             `Table(`id(`TrustedNetworks), `header(_("Network")),Network_items),
             `SelectionBox(`id(`TrustedNetworks), "",Networks),
             `PushButton(`id(`DeleteNetwork), _("Delete"))
            )
          )
       )
     ),
    `Frame(_("Security Settings for Sending Mail via the Server"),security)
   );
  return content;
}

term MailLocalDeliveryDialog() {
  boolean TypeCYRUS    = ((string)MailServer::MailLocalDelivery["Type"]:"" == "cyrus");
  boolean TypePROCMAIL = ((string)MailServer::MailLocalDelivery["Type"]:"" == "procmail");
  boolean TypeLOCAL    = ((string)MailServer::MailLocalDelivery["Type"]:"" == "local");
  boolean TypeNONE     = ((string)MailServer::MailLocalDelivery["Type"]:"" == "none");
  boolean limit        = ((string)MailServer::MailLocalDelivery["MailboxSizeLimit"]:"0" != "0");

  term settings = `HBox();
  if(TypeCYRUS){
    integer MailboxSizeLimit = tointeger((string)MailServer::MailLocalDelivery["MailboxSizeLimit"]:"0");
    integer QuotaLimit       = tointeger((string)MailServer::MailLocalDelivery["QuotaLimit"]:"0");
    integer ImapIdleTime     = tointeger((string)MailServer::MailLocalDelivery["ImapIdleTime"]:"0");
    integer PopIdleTime      = tointeger((string)MailServer::MailLocalDelivery["PopIdleTime"]:"0");
    integer MAXINT           = 10000000000000;
    boolean HardQuotaLimit   = ((string)MailServer::MailLocalDelivery["HardQuotaLimit"]:"0" == "1");
    boolean AlternateNameSpace   = ((string)MailServer::MailLocalDelivery["AlternateNameSpace"]:"0" == "1");
    string  FallBackMailbox  = (string)MailServer::MailLocalDelivery["FallBackMailbox"]:"root";
    string  Security         = (string)MailServer::MailLocalDelivery["Security"]:"TLS";
    boolean PLAIN            = (Security == "PLAIN");
    boolean SSL              = (Security == "SSL");
    boolean TLS              = (Security == "TLS");
    settings = `Frame(_("IMAP Settings"),
      `VBox(
        `Frame(_("Security"),
	  `RadioButtonGroup(`id(`Security),
	    `HBox(
	      `RadioButton(`id("PLAIN"), _("Plain"),  PLAIN),
	      `HStretch(),
	      `RadioButton(`id("SSL"), _("SSL (imaps)"),  SSL),
	      `HStretch(),
	      `RadioButton(`id("TLS"), _("TLS (imaps via imap)"),  TLS)
	    )
	  )
	),
	`HBox(
	  `VBox(
	     `HBox(`Label(_("Default Mailbox Size")),`IntField(`id(`MailboxSizeLimit),"KB",0,MAXINT,MailboxSizeLimit)),
	     `HBox(`Label(_("Quota Warning Limit")), `IntField(`id(`QuotaLimit),      "%",0,100,QuotaLimit)),
	     `HBox(`Label(_("IMAP Idle Time")),      `IntField(`id(`ImapIdleTime),    "Min",30,100,ImapIdleTime)),
	     `HBox(`Label(_("IMAP Idle Time")),      `IntField(`id(`PopIdleTime),     "Min",10,100,PopIdleTime))
	   ),
	  `VBox(
	     `Left(`CheckBox(`id(`HardQuotaLimit),    _("Hard Quota Limit"),HardQuotaLimit)),
	     `Left(`CheckBox(`id(`AlternateNameSpace),_("Use Alternate Name Space"),AlternateNameSpace)),
	     `Left(`TextEntry(`id(`FallBackMailbox),  _("Fall Back Mailbox"), FallBackMailbox))
	  )
	)
      )
    );
  } else if(TypeLOCAL){
    string  SpoolDirectory = (string)MailServer::MailLocalDelivery["SpoolDirectory"]:"";
    boolean mdir     = regexpmatch(SpoolDirectory,"/$");
    boolean varspool = regexpmatch(SpoolDirectory,"var");
    boolean homedir  = regexpmatch(SpoolDirectory,"HOME/Mail");
//    boolean custom   = (regexpmatch(SpoolDirectory,"HOME") && !homedir);
    term MailboxSizeTerm = `TextEntry(`id(`MailboxSizeLimit), "", (string)MailServer::MailLocalDelivery["MailboxSizeLimit"]:"0");
    if(!limit) {
         MailboxSizeTerm = `TextEntry(`id(`MailboxSizeLimit), `opt(`disabled),"", (string)MailServer::MailLocalDelivery["MailboxSizeLimit"]:"0");
    }

    settings = `Frame(_("Mailbox Settings"),
       `VBox(
	 `Frame(_("Type"),
	   `RadioButtonGroup(`id(`MailboxType),
	     `HBox(
		`RadioButton(`id("mbox"), _("Maibox"),  !mdir),
	        `HStretch(),
		`RadioButton(`id("mdir"), _("Maildir"), mdir),
	        `HStretch()
	      )
	    )
	  ),
         `Frame(_("Repository"),
	   `RadioButtonGroup(`id(`Repository),
	     `HBox(
	        `Left(`RadioButton(`id("varspool"), "/var/spool/mail/$USER",  varspool)),
	        `HStretch(),
	        `Left(`RadioButton(`id("homedir"),  "$HOME/Mail",              homedir)),
	        `HStretch()
//		`HBox(`Left(`RadioButton(`id("custom"),   "$HOME/",             custom)),
//		      `Left(`TextEntry(`id(`customdir),     "", ""))
//		     )
	      )
	    )
	  ),
	 `Frame(_("Mailbox Size"),
	   `RadioButtonGroup(`id(`MailboxSize),
	   `HBox(
	     `RadioButton(`id("NoLimit"), `opt (`notify), _("No Limit"), !limit),
	     `RadioButton(`id("Limit"), `opt (`notify), _("Mailbox Size"), limit),
	     MailboxSizeTerm,
	     `Label("KByte")
	    )
	  )
	),
	 `HStretch()
        )
    );
  }

  term content = `VBox(
      `Frame(_("Local Delivery Type"),
         `RadioButtonGroup(`id(`Type),
            `HBox(
	        `RadioButton(`id("cyrus"),    `opt(`notify), _("Cyrus IMAP"), TypeCYRUS),
		`HStretch(),
	        `RadioButton(`id("procmail"), `opt(`notify), _("Procmail"),   TypePROCMAIL),
		`HStretch(),
	        `RadioButton(`id("local"),    `opt(`notify), _("Filesystem"), TypeLOCAL),
		`HStretch(),
	        `RadioButton(`id("none"),     `opt(`notify), _("No local Delivery"), TypeNONE)
	     )
          )
      ),
      settings,
      `VStretch()
   );
  return content;
}

term FetchingMailDialog() {
  list<term>   table_items  = [];
  integer      IID          = 0;
  list<map<string,string> > entries = (list<map<string,string> >) MailServer::FetchingMail["Items"]:[];
  boolean      FetchByDialIn   = ((string) MailServer::FetchingMail["FetchByDialIn"]:""   == "1");
  boolean      FetchMailSteady = ((string) MailServer::FetchingMail["FetchMailSteady"]:"" == "1");
  //boolean      FetchByDialIn   = (boolean)MailServer::FetchingMail["FetchByDialIn"]:false;
  //boolean      FetchMailSteady = (boolean)MailServer::FetchingMail["FetchMailSteady"]:false;
  integer      FetchingInterval= tointeger((string) MailServer::FetchingMail["FetchingInterval"]:"");

  foreach( map<string,string> entry, entries, ``{
    term item = `item(`id(IID), entry["server"]:"", entry["protocol"]:"",entry["remote_user"]:"",entry["local_user"]:"");
    table_items = add(table_items,item);
    IID = IID+1;
  });

  term FetchingIntervalTerm = `HBox(
                `CheckBox(`id(`FetchMailSteady),`opt(`notify),_("Fetch Mail Steady"),FetchMailSteady),
		`Label(_("Fetching Intervall")),
                `IntField(`id(`FetchingInterval), "Min",10,120,FetchingInterval),
                `HStretch()
       );
  if(!FetchMailSteady) {
         FetchingIntervalTerm = `HBox(
	        `CheckBox(`id(`FetchMailSteady),`opt(`notify),_("Fetch Mail Steady"),FetchMailSteady),
                `Label(`opt(`disabled),_("Fetching Intervall")),
                `IntField(`id(`FetchingInterval), `opt(`disabled),"Min",10,120,FetchingInterval),
                `HStretch()
         );
  }

  term content = `VBox(
	   `Frame(_("Mail Fetching Scheudler"),
	     `VBox(
	       `Left(`CheckBox(`id(`FetchByDialIn),  `opt(`notify),_("Fetch Mail by Connecting Internet."),FetchByDialIn)),
	       `Left(FetchingIntervalTerm)
	     )
           ),
           `HBox(
	     `Frame(_("Defined  Fetchmail Jobs"),
	       `Table(`id(`table), `opt(`keepSorting),`header(_("Server"),_("Protocol"),_("User"),_("Local User")),table_items)
	     ),
	     `VBox(
	       `PushButton(`id(`Add),   _("Add")),
//	       `PushButton(`id(`Edit),  _("Edit")),
	       `PushButton(`id(`Delete),_("Delete"))
	     )
	   )
   );
/*  term FetchingIntervalTerm = `HBox(`Label(_("Fetching Intervall")), `IntField(`id(`FetchingInterval), "Min",10,120,FetchingInterval));
  if(!FetchMailSteady) {
    FetchingIntervalTerm = `HBox(`Label(`opt(`disabled),_("Fetching Intervall")), `IntField(`id(`FetchingInterval), `opt(`disabled),"Min",10,120,FetchingInterval));
  }

  term content = `HBox(
	   `HWeight(2,`VBox(
	     `Frame(_("Defined  Fetchmail Jobs"),
	       `Table(`id(`table), `header(_("Server"),_("Protocol"),_("User"),_("Local User")),table_items)
	     ),
	     `HBox(
	       `PushButton(`id(`Add),   _("Add")),
	       `PushButton(`id(`Edit),  _("Edit")),
	       `PushButton(`id(`Delete),_("Delete"))
	     )
	   )),
	   `HWeight(1,`Frame(_("Mail Fetching Scheudler"),
	     `VBox(
	       `Left(`CheckBox(`id(`FetchByDialIn),  `opt(`notify),_("Fetch Mail by Connecting Internet."),FetchByDialIn)),
	       `Left(`CheckBox(`id(`FetchMailSteady),`opt(`notify),_("Fetch Mail Steady"),FetchMailSteady)),
	       `Left(FetchingIntervalTerm),
               `VStretch()
	     )
	   ))
   );*/
  return content;
}

map FetchmailAddItem(){
   UI::OpenDialog(`opt(`decorated ),  `Frame(_("Add New Fetchmail Job"),
            `VBox(
              `TextEntry(`id(`server),       "Server Address",  ""),
	      `ComboBox (`id(`protocol),     _("Protocol"), [ "AUTO", "POP2", "POP3", "IMAP", "APOP", "KPOP", "SDPS", "ETRN", "ODMR", ]),
              `TextEntry(`id(`remote_user),  "Remote User",  ""),
              `TextEntry(`id(`password),     "Remote User's Password",  ""),
              `TextEntry(`id(`local_user),   "Local User",  ""),
              `HBox(
	         `PushButton(`id(`cancel),   _("Cancel")),
	         `PushButton(`id(`ok),   _("OK"))
               )
	     )
	   )
    );
    any ret       = nil;
    map Job       = $[];
    while(ret != `cancel && ret != `ok )
    {
        map event = UI::WaitForEvent();
        ret       = event["ID"]:nil;
	if( ret == `ok) {
	       Job["server"]      = UI::QueryWidget(`id(`server), `Value);
	       Job["protocol"]    = UI::QueryWidget(`id(`protocol), `Value);
	       Job["remote_user"] = UI::QueryWidget(`id(`remote_user), `Value);
	       Job["password"]    = UI::QueryWidget(`id(`password), `Value);
	       Job["local_user"]  = UI::QueryWidget(`id(`local_user), `Value);
               Job["other_server_options"] = "";
               Job["other_user_options"] = "";
               Job["enabled"] = true;
	}
    }
    UI::CloseDialog();
    return Job;

}

term MailLocalDomainsDialog() {
  list<term> Domains = [];
  foreach(map Domain, MailServer::MailLocalDomains["Domains"]:[], ``{
      Domains = add( Domains, `item(`id(Domain["Name"]:""),
                                     Domain["Name"]:"",
				     Domain["Type"]:"",
				     Domain["Masquerading"]:""
                   ));
  });
y2milestone("Domains %1", Domains);
  term content = `VBox (
       `VBox(
         `Frame(_("Define New My Domain"),
           `HBox(
               `TextEntry(`id(`Name),     "Name", ""),
	       `HStretch(),
	       `ComboBox (`id(`Type), _("Type"), ["virtual","local"]),
	       `CheckBox(`id(`Masquerading),_("Masquerading"),true),
               `PushButton(`id(`Add), _("Add"))
            )
          ),
         `Frame(_("Defined My Domains"),
           `HBox(
             `Table(`id(`table), `header(_("Domain"), _("Type"), _("Masquerading")),Domains),
             `PushButton(`id(`Delete), _("Delete"))
            )
          )
       )
   );
  return content;
}

any ComplexDialog() {

   Wizard::CreateTreeDialog();
   list<map> Tree = GenerateTree([], "", MailServer::ModulesTreeContent) ;
   Wizard::CreateTree(Tree, _("&Mail Server Configuration Modules"));

   string helptext = "Bla Bla Bla";
   term content = GlobalSettingsDialog();
   string FocusedContent = "GlobalSettings";

   string CID = ""; //CurrentItem
   string CIDRBL = ""; //CurrentItem

   Wizard::SetContents(_("Mail Server Configuration"), content ,helptext, true, true);
   UI::WizardCommand(`SetBackButtonLabel( "" ) );
   UI::WizardCommand(`SetNextButtonLabel( _("Save") ) );

   any ret       = nil;
   any EventType = nil;
   while(ret != `next )
   {
        boolean focus_changed     = false;
        string  OldFocusedContent = FocusedContent;
        map event = UI::WaitForEvent();
        ret       = event["ID"]:nil;
        EventType = event["EventType"]:nil;

        if(ret == `cancel || ret == `abort ) {
            if(ReallyAbort()) break;
            else continue;
        }
        //If anithing happenend we save the changes into the global maps
        if(FocusedContent == "GlobalSettings") {
           MailServer::GlobalSettings["Changed"]            = true;
           if(UI::QueryWidget(`id(`MailSize), `CurrentButton) == "MailSizeLimit") {
             MailServer::GlobalSettings["MaximumMailSize"]    = UI::QueryWidget(`id(`MaximumMailSize), `Value);
             if(UI::QueryWidget(`id(`MaximumMailSize), `Value) == "0" ) {
               MailServer::GlobalSettings["MaximumMailSize"]    = "1";
             }
           } else {
             MailServer::GlobalSettings["MaximumMailSize"]    = "0";
           }
           MailServer::GlobalSettings["Banner"]             = UI::QueryWidget(`id(`Banner), `Value);
           MailServer::GlobalSettings["SendingMail","Type"] = UI::QueryWidget(`id(`SendingMailType), `CurrentButton);
           MailServer::GlobalSettings["SendingMail","TLS"]  = UI::QueryWidget(`id(`SendingMailTLS), `CurrentButton);
           MailServer::GlobalSettings["SendingMail","RelayHost","Name"]     = "";
           MailServer::GlobalSettings["SendingMail","RelayHost","Auth"]     = "0";
           MailServer::GlobalSettings["SendingMail","RelayHost","Account"]  = "";
           MailServer::GlobalSettings["SendingMail","RelayHost","Password"] = "";

           if(MailServer::GlobalSettings["SendingMail","Type"]:"relayhost" == "relayhost"){
              MailServer::GlobalSettings["SendingMail","RelayHost","Name"] = UI::QueryWidget(`id(`RelayHostName), `Value);
              if((boolean)UI::QueryWidget(`id(`RelayHostAuth), `Value)){
                string password1 = (string)UI::QueryWidget(`id(`Password1), `Value);
                string password2 = (string)UI::QueryWidget(`id(`Password2), `Value);
                if( password1 != password2) {
                    Report::Error(_("The passwords are not identical."));
                    continue;
                }
                MailServer::GlobalSettings["SendingMail","RelayHost","Auth"]     = "1";
                MailServer::GlobalSettings["SendingMail","RelayHost","Account"]  = UI::QueryWidget(`id(`RelayHostAccount), `Value);
                MailServer::GlobalSettings["SendingMail","RelayHost","Password"] = password1;
              }
           }
           content        = GlobalSettingsDialog();
           helptext       = HELPS["GlobalSettings"]:"Bla Bla Bla";
        } else if(FocusedContent == "MailTransports") {
           if( ret == `Auth ) {
	      if((boolean)UI::QueryWidget(`id(`Auth), `Value)){
	        UI::ChangeWidget(`id(`Account),   `Enabled, true);
		UI::ChangeWidget(`id(`Password1), `Enabled, true);
		UI::ChangeWidget(`id(`Password2), `Enabled, true);
	      } else {
	        UI::ChangeWidget(`id(`Account),   `Enabled, false);
		UI::ChangeWidget(`id(`Password1), `Enabled, false);
		UI::ChangeWidget(`id(`Password2), `Enabled, false);
	      }
	      continue;
           }
           MailServer::MailTransports["Changed"] = true;
	   list<map> Transports  = (list<map>) MailServer::MailTransports["Transports"]:[];
	   if(size ((list) MailServer::MailTransports["Transports"]:[]) > 0) {
	         CID = (string)(UI::QueryWidget(`id(`table), `CurrentItem));
           }
           //Now we generate the new/edited entry
           string password1 = (string)UI::QueryWidget(`id(`Password1), `Value);
           string password2 = (string)UI::QueryWidget(`id(`Password2), `Value);
	   map NewTransport = $[];
           NewTransport["Destination"] = (string)UI::QueryWidget(`id(`Destination), `Value);
           NewTransport["Transport"]   = (string)UI::QueryWidget(`id(`Protocol), `Value);
           NewTransport["Nexthop"]     = (string)UI::QueryWidget(`id(`Server), `Value);
           if(NewTransport["Transport"]:"smtp" != "error" ) {
             NewTransport["TLS"]         = (string)UI::QueryWidget(`id(`TLS), `CurrentButton);
             if((boolean)UI::QueryWidget(`id(`Auth), `Value)){
               NewTransport["Auth"]      = "1";
               NewTransport["Account"]   = (string)UI::QueryWidget(`id(`Account), `Value);
               NewTransport["Password"]  = password1;
             }
             if((boolean)UI::QueryWidget(`id(`NOMX), `Value)){
               NewTransport["Nexthop"] = "["+NewTransport["Nexthop"]:""+"]";
             }
           }
           if(ret == `Add || ret == `Edit) {
           //TODO Checking if the attributes are correct & that Destination is unique
               if(regexpmatch(NewTransport["Destination"]:"","[ öä#+ü]") || NewTransport["Destination"]:"" == ""){
                    Report::Error(_("The destination is invalid."));
                    continue;
               }
               if(ret == `Add) {
                  list TR = [];
                  foreach(map Transport, Transports, ``{
                    TR = add(TR,Transport["Destination"]:"");
                  });
                  //TODO search for .<domainname to>
		  if(contains( TR, NewTransport["Destination"]:"")) {
                     Report::Error(_("There is already a transport defined for this destination."));
                     continue;
		  }
               }
           }
	   if(ret == `Add) {
              if((boolean)UI::QueryWidget(`id(`Auth), `Value)){
                if( password1 != password2) {
                    Report::Error(_("The passwords are not identical."));
                    continue;
                }
              }
	      MailServer::MailTransports["Transports"] = [];
	      foreach(map Transport, Transports, ``{
	        MailServer::MailTransports["Transports"] = add(MailServer::MailTransports["Transports"]:[],Transport);
		if(Transport["Destination"]:"" == CID) {
	          MailServer::MailTransports["Transports"] = add(MailServer::MailTransports["Transports"]:[],NewTransport);
                  if((boolean)UI::QueryWidget(`id(`Subdomains), `Value)){
                    NewTransport["Destination"] = "."+NewTransport["Destination"]:"";
	            MailServer::MailTransports["Transports"] = add(MailServer::MailTransports["Transports"]:[],NewTransport);
                  }
		}
	      });
	      if(size ((list) MailServer::MailTransports["Transports"]:[]) == 0) {
	        MailServer::MailTransports["Transports"] = add(MailServer::MailTransports["Transports"]:[],NewTransport);
                if((boolean)UI::QueryWidget(`id(`Subdomains), `Value)){
                   NewTransport["Destination"] = "."+NewTransport["Destination"]:"";
	           MailServer::MailTransports["Transports"] = add(MailServer::MailTransports["Transports"]:[],NewTransport);
                }
	      }
              CID = NewTransport["Destination"]:"";
	   } else if(ret == `Edit) {
              if((boolean)UI::QueryWidget(`id(`Auth), `Value)){
                if( password1 != password2) {
                    Report::Error(_("The passwords are not identical."));
                    continue;
                }
              }
	      MailServer::MailTransports["Transports"] = [];
	      foreach(map Transport, Transports, ``{
		if(Transport["Destination"]:"" == CID) {
	          MailServer::MailTransports["Transports"] = add(MailServer::MailTransports["Transports"]:[],NewTransport);
		} else {
	          MailServer::MailTransports["Transports"] = add(MailServer::MailTransports["Transports"]:[],Transport);
                }
	      });
              CID = NewTransport["Destination"]:"";
	   } else if (ret == `Delete) {
	      MailServer::MailTransports["Transports"] = [];
	      foreach(map Transport, Transports, ``{
		if(Transport["Destination"]:"" != CID) {
                  //TODO search for .<domainname to>
	           MailServer::MailTransports["Transports"] = add(MailServer::MailTransports["Transports"]:[],Transport);
		}
	      });
	   } else if (ret == `Clean) {
               CID = "";
           }
	   content        = MailTransportsDialog(CID);
	   helptext       = HELPS["MailTransports"]:"Bla Bla Bla";
        } else if(FocusedContent == "MailPrevention") {
	  if(ret == `VirusScanning || ret == "off" || ret == "medium" || ret == "hard") {
	    MailServer::MailPrevention["Changed"] = true;
            continue;
          }
	  if(ret == `RBLAdd) {
	    string NewRLBServer = RBLAdd();
	    if( NewRLBServer != "") {
	      MailServer::MailPrevention["RBLList"] = add((list<string>)MailServer::MailPrevention["RBLList"]:[],NewRLBServer);
	      CIDRBL = NewRLBServer;
	      MailServer::MailPrevention["Changed"] = true;
	    }
	  }
	  if(ret == `AccessAdd) {
	    map SenderRestriction = AccessAdd();
	    if( SenderRestriction["MailClient"]:"" != "" && SenderRestriction["MailAction"]:"" != "" ) {
	       MailServer::MailPrevention["AccessList"] = add((list<map>)MailServer::MailPrevention["AccessList"]:[],SenderRestriction);
	    }
	    CID = SenderRestriction["MailClient"]:"";
	  }else if(ret == `RBLDelete){
	    string Server = (string)UI::QueryWidget(`id(`RBLList),`CurrentItem);
	    list<string>  RBLList =  (list<string>)MailServer::MailPrevention["RBLList"]:[];
	    MailServer::MailPrevention["RBLList"] = [];
	    foreach(string my_item, RBLList, ``{
	       if( my_item != Server) {
	          MailServer::MailPrevention["RBLList"] = add(MailServer::MailPrevention["RBLList"]:[],my_item);
	       }
	    });
	  }else if(ret == `AccessDelete){
	    string Sender = (string)(UI::QueryWidget(`id(`table), `CurrentItem));
	    list<map<string,string> > AccessList = (list<map<string,string> >)MailServer::MailPrevention["AccessList"]:[];
	    MailServer::MailPrevention["AccessList"] = [];
	    foreach(map<string,string> my_item, AccessList, ``{
	       if( my_item["MailClient"]:"" != Sender ){
	         MailServer::MailPrevention["AccessList"] = add(MailServer::MailPrevention["AccessList"]:[],my_item);
	       }
	    });
	  }
          MailServer::MailPrevention["BasicProtection"] = (string)UI::QueryWidget(`id(`BasicProtection), `CurrentButton);
          if((boolean) UI::QueryWidget(`id(`VirusScanning), `Value)) {
             MailServer::MailPrevention["VirusScanning"] = "1";
          } else {
             MailServer::MailPrevention["VirusScanning"] = "0";
          }
          content        = MailPreventionDialog(CID,CIDRBL);
          helptext       = HELPS["MailPrevention"]:"Bla Bla Bla";
        } else if(FocusedContent == "MailRelaying") {
           MailServer::MailRelaying["Changed"]            = true;
	   string SMTPDTLSMode = (string)UI::QueryWidget(`id(`SMTPDTLSMode), `CurrentButton);
           if(ret == `AddNewNetwork) {
             string NewNetwork = (string)UI::QueryWidget(`id(`NewNetwork), `Value);
             //TODO Checking if network is a real network
             MailServer::MailRelaying["TrustedNetworks"] = add((list<string>)MailServer::MailRelaying["TrustedNetworks"]:[],NewNetwork);
           }
	   if(ret == `DeleteNetwork){
	     string CurrentNetwork = (string)UI::QueryWidget(`id(`TrustedNetworks), `CurrentItem);
	     MailServer::MailRelaying["TrustedNetworks"] = filter(string network, MailServer::MailRelaying["TrustedNetworks"]:[],
	                                                          {return network != CurrentNetwork;});
	   }
           MailServer::MailRelaying["SMTPDTLSMode"]  = SMTPDTLSMode;
           if((boolean)UI::QueryWidget(`id(`RequireSASL), `Value)){
             MailServer::MailRelaying["RequireSASL"] = "1";
           } else {
             MailServer::MailRelaying["RequireSASL"] = "0";
           }
           if((boolean)UI::QueryWidget(`id(`UserRestriction), `Value)){
	     //TODO It must be fixed!!!!
             MailServer::MailRelaying["UserRestriction"] = "1";
             MailServer::MailRelaying["RequireSASL"]     = "1";
	     if(SMTPDTLSMode != "enforce" && SMTPDTLSMode != "auth_only") {
	        MailServer::MailRelaying["SMTPDTLSMode"]    = "enforce";
             }
           } else {
             MailServer::MailRelaying["UserRestriction"] = "0";
           }
           content        = MailRelayingDialog();
           helptext       = HELPS["MailRelaying"]:"Bla Bla Bla";
        } else if(FocusedContent == "MailLocalDelivery") {
           MailServer::MailLocalDelivery["Changed"]  = true;
           MailServer::MailLocalDelivery["Type"] = (string)UI::QueryWidget(`id(`Type), `CurrentButton);
	   if( UI::WidgetExists(`id(`Repository)) && UI::WidgetExists(`id(`MailboxType)) && UI::WidgetExists(`id(`MailboxSize))) {
		   if(UI::QueryWidget(`id(`Repository), `CurrentButton) == "varspool") {
		      MailServer::MailLocalDelivery["SpoolDirectory"] = "/var/spool/mail";
		   } else {
		      MailServer::MailLocalDelivery["SpoolDirectory"] = "$HOME/Mail";
		   }
		   if(UI::QueryWidget(`id(`MailboxType), `CurrentButton) == "mdir") {
		      MailServer::MailLocalDelivery["SpoolDirectory"] = MailServer::MailLocalDelivery["SpoolDirectory"]:""+"/";
		   }
		   if(UI::QueryWidget(`id(`MailboxSize), `CurrentButton) == "NoLimit") {
		      MailServer::MailLocalDelivery["MailboxSizeLimit"] = "0";
		   } else {
		      MailServer::MailLocalDelivery["MailboxSizeLimit"] = UI::QueryWidget(`id(`MailboxSizeLimit), `Value);
		      if(MailServer::MailLocalDelivery["MailboxSizeLimit"]:"0" == "0") {
			 MailServer::MailLocalDelivery["MailboxSizeLimit"] = "1";
		      }
		   }
	   }
	   if( UI::WidgetExists(`id(`Security)) && UI::WidgetExists(`id(`QuotaLimit)) && UI::WidgetExists(`id(`HardQuotaLimit))) {
	     MailServer::MailLocalDelivery["MailboxSizeLimit"] = tostring(UI::QueryWidget(`id(`MailboxSizeLimit), `Value));
	     MailServer::MailLocalDelivery["QuotaLimit"]       = tostring(UI::QueryWidget(`id(`QuotaLimit), `Value));
	     MailServer::MailLocalDelivery["ImapIdleTime"]     = tostring(UI::QueryWidget(`id(`ImapIdleTime), `Value));
	     MailServer::MailLocalDelivery["PopIdleTime"]      = tostring(UI::QueryWidget(`id(`PopIdleTime), `Value));
	     MailServer::MailLocalDelivery["FallBackMailbox"]  = UI::QueryWidget(`id(`FallBackMailbox), `Value);
	     MailServer::MailLocalDelivery["Security"]         = UI::QueryWidget(`id(`Security), `CurrentButton);
	     if((boolean)UI::QueryWidget(`id(`HardQuotaLimit), `Value)) {
	         MailServer::MailLocalDelivery["HardQuotaLimit"]      = "1";
	     } else {
	         MailServer::MailLocalDelivery["HardQuotaLimit"]      = "0";
	     }
	     if((boolean)UI::QueryWidget(`id(`AlternateNameSpace), `Value)) {
	         MailServer::MailLocalDelivery["AlternateNameSpace"]      = "1";
	     } else {
	         MailServer::MailLocalDelivery["AlternateNameSpace"]      = "0";
	     }
	   }
           content        = MailLocalDeliveryDialog();
           helptext       = HELPS["MailLocalDelivery"]:"Bla Bla Bla";
        } else if(FocusedContent == "FetchingMail") {
           MailServer::FetchingMail["Changed"]  = true;
	   if(ret == `Add) {
	     map Job = FetchmailAddItem();
	     if(Job["server"]:"" != "" && Job["password"]:"" != "" && Job["remote_user"]:"" != "" && Job["local_user"]:"" != "") {
	         MailServer::FetchingMail["Items"] = add((list<map>) MailServer::FetchingMail["Items"]:[],Job);
	     }
	   } else if(ret == `Delete) {
             integer IID    = (integer)(UI::QueryWidget(`id(`table), `CurrentItem));
             integer my_id  = 0;
             list<map> Jobs = (list<map>) MailServer::FetchingMail["Items"]:[];
             MailServer::FetchingMail["Items"] = [];
	     foreach( map Job, Jobs, ``{
	       if(my_id != IID) {
	         MailServer::FetchingMail["Items"] = add(MailServer::FetchingMail["Items"]:[],Job);
	       }
               my_id = my_id+1;
	     });
	   } else if(ret == `FetchByDialIn ){
             if((boolean)UI::QueryWidget(`id(`FetchByDialIn), `Value)){
                MailServer::FetchingMail["FetchByDialIn"] = "1";
             } else {
                MailServer::FetchingMail["FetchByDialIn"] = "0";
             }
	   } else if(ret == `FetchMailSteady ){
             if((boolean)UI::QueryWidget(`id(`FetchMailSteady), `Value)){
                MailServer::FetchingMail["FetchMailSteady"] = "1";
             } else {
                MailServer::FetchingMail["FetchMailSteady"] = "0";
             }
           }
           if(UI::WidgetExists(`id(`FetchingInterval))) {
             MailServer::FetchingMail["FetchingInterval"] = tostring(UI::QueryWidget(`id(`FetchingInterval), `Value));
           }
           content        = FetchingMailDialog();
           helptext       = HELPS["FetchingMail"]:"Bla Bla Bla";
        } else if(FocusedContent == "MailLocalDomains") {
	   MailServer::MailLocalDomains["Changed"] = true;
	   if(ret == `Delete) {
	     list<map> Domains = (list<map>)  MailServer::MailLocalDomains["Domains"]:[];
             CID   = (string)UI::QueryWidget(`id(`table), `CurrentItem);
	     MailServer::MailLocalDomains["Domains"] = [];
             foreach(map Domain, Domains, ``{
	        if( CID != Domain["Name"]:"" ) {
                  MailServer::MailLocalDomains["Domains"] = add( MailServer::MailLocalDomains["Domains"]:[], Domain);
		}
              });
           } else if(ret == `Add) {
	      string Name         = (string)UI::QueryWidget(`id(`Name), `Value);
	      string Type         = (string)UI::QueryWidget(`id(`Type), `Value);
	      string Masquerading = "no"; 
	      if((boolean)UI::QueryWidget(`id(`Masquerading), `Value)) {
	        Masquerading = "yes";
	      }
              MailServer::MailLocalDomains["Domains"] = add (MailServer::MailLocalDomains["Domains"]:[],
                                         $[ "Name"         : Name,
					    "Type"         : Type,
					    "Masquerading" : Masquerading
                                          ]);
           }
           content        = MailLocalDomainsDialog();
           helptext       = HELPS["MailLocalDomains"]:"Bla Bla Bla";
        }
        if(ret == "GlobalSettings" && FocusedContent != "GlobalSettings") {
           content        = GlobalSettingsDialog();
           helptext       = HELPS["GlobalSettings"]:"Bla Bla Bla";
           focus_changed  = true;
           FocusedContent ="GlobalSettings";
        } else if(ret == "MailTransports" && FocusedContent != "MailTransports") {
           content        = MailTransportsDialog("");
           helptext       = HELPS["MailTransports"]:"Bla Bla Bla";
           focus_changed  = true;
           FocusedContent ="MailTransports";
        } else if(ret == "MailPrevention" && FocusedContent != "MailPrevention") {
           content        = MailPreventionDialog(CID,CIDRBL);
           helptext       = HELPS["MailPrevention"]:"Bla Bla Bla";
           focus_changed  = true;
           FocusedContent ="MailPrevention";
        } else if(ret == "MailRelaying" && FocusedContent != "MailRelaying") {
           content        = MailRelayingDialog();
           helptext       = HELPS["MailRelaying"]:"Bla Bla Bla";
           focus_changed  = true;
           FocusedContent ="MailRelaying";
        } else if(ret == "MailLocalDelivery" && FocusedContent != "MailLocalDelivery") {
           content        = MailLocalDeliveryDialog();
           helptext       = HELPS["MailLocalDelivery"]:"Bla Bla Bla";
           focus_changed  = true;
           FocusedContent ="MailLocalDelivery";
        } else if(ret == "FetchingMail" && FocusedContent != "FetchingMail") {
           content        = FetchingMailDialog();
           helptext       = HELPS["FetchingMail"]:"Bla Bla Bla";
           focus_changed  = true;
           FocusedContent ="FetchingMail";
        } else if(ret == "MailLocalDomains" && FocusedContent != "MailLocalDomains") {
           content        = MailLocalDomainsDialog();
           helptext       = HELPS["MailLocalDomains"]:"Bla Bla Bla";
           focus_changed  = true;
           FocusedContent ="MailLocalDomains";
        }
y2milestone("event %1",event);
y2milestone("event %1",ret);
y2milestone("FocusedContent %1",FocusedContent);
        if(ret != `cancel && ret != `abort && ret != `next ) {
          Wizard::SetContents(_("Mail Server Configuration"), content ,helptext, false, true);
          UI::WizardCommand(`SetBackButtonLabel( "" ) );
          UI::WizardCommand(`SetNextButtonLabel( _("Save") ) );
	  UI::ChangeWidget(`id(`table), `CurrentItem, CID);
        }
   }

   return ret;
}

/**
 * Configure1 dialog
 * @return dialog result
 */
any Configure1Dialog () {

    /* MailServer configure1 dialog caption */
    string caption = _("MailServer Configuration");

    /* MailServer configure1 dialog contents */
    term contents = `Label (_("First part of configuration of mail-server"));

    Wizard::SetContentsButtons(caption, contents, HELPS["c1"]:"",
	    Label::BackButton(), Label::NextButton());

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back) {
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

/**
 * Configure2 dialog
 * @return dialog result
 */
any Configure2Dialog () {

    /* MailServer configure2 dialog caption */
    string caption = _("MailServer Configuration");

    /* MailServer configure2 dialog contents */
    term contents = `Label (_("Second part of configuration of mail-server"));

    Wizard::SetContentsButtons(caption, contents, HELPS["c2"]:"",
	    Label::BackButton(), Label::NextButton());

    any ret = nil;
    while(true) {

	ret = UI::UserInput();

	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `back) {
            break;
        }
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }

    return ret;
}

/* EOF */
}
